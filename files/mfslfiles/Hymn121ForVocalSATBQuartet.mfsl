#!//Users/jacquesmenu/musicformats-git-dev/build/bin/mfsl

###
  This MFSL script contains:
    - a tool specification, telling which MusicFormats tool
      to use, here xml2ly;

    - an input specification, the input source to apply the latter to,
      here file name "Hymn121.xml";

    - a number of MusicFormats options;

    - a SCORE choice specification containing 5 label,
      each telling which options to use
      to get a score generated with this script and xml2ly/LilyPond;

    - a 'select' specification to choose one such SCORE label;
###


# ----------------------------------------------------------
# the MusicFormats tool to be used
# ----------------------------------------------------------

tool : xml2ly


# ----------------------------------------------------------
# the input file to be handled by the tool
# ----------------------------------------------------------

input : "Hymn121.xml"


# ----------------------------------------------------------
# some commmon options
# ----------------------------------------------------------

# header
  -title "Hymn 121 - Angels From The Realms Of Glory"

# display
#   -display-options-values

# LilyPond
  -lilypond-generation-infos

# dynamics
  -all-wedges-below


# ----------------------------------------------------------
# the labels for the SCORE choice
# ----------------------------------------------------------

# choose names that match the intent,
# uppercase to emphasize a choice name is a matter of taste

choice SCORE :
  tutti | soprano | alto | tenor | bass,

  default: tutti
;


# ----------------------------------------------------------
# tell which options are to be used depending on SCORE labels
# ----------------------------------------------------------

case SCORE :
	tutti:
	  -output-file-name "Hymn121_SATB_tutti.ly"

    -global-staff-size 17.675
    -subtitle "Tutti"
  ;

	soprano:
	  -output-file-name "Hymn121_SATB_soprano.ly"

    -subtitle "Soprano"
    -keep-musicxml-part-id P1
    -msr-rename-part "P1:soprano"

    -display-lpsr-short
  ;

	alto:
	  -output-file-name "Hymn121_SATB_alto.ly"

    -subtitle "Alto"
    -keep-musicxml-part-id P2
  ;

	tenor:
	  -output-file-name "Hymn121_SATB_tenor.ly"

    -subtitle "Tenor"
    -keep-musicxml-part-id P3

    -global-staff-size 23 # a more readable score is needed
  ;

	bass:
	  -output-file-name "Hymn121_SATB_bass.ly"

    -subtitle "Bass"
    -keep-musicxml-part-id P4

    -ignore-musicxml-lyrics # there's a glitch at the last measure
  ;
;


# ----------------------------------------------------------
# select a particular SCORE label to generate the corresponding score
# ----------------------------------------------------------

###
  The 'select' statements below override
  the default "tutti" label setting in the 'choice' statement.

  Comment/uncomment them to select either the SCORE to be generated

  They can also be overridden by options to the MFSL interpreter, such as:
    -select SCORE:tenor
  or:
    -all SCORE
###

select SCORE : tutti ; # superflous actually
# select SCORE : soprano ;
# select SCORE : alto ;
# select SCORE : tenor ;
# select SCORE : bass ;

# all SCORE ;

#
