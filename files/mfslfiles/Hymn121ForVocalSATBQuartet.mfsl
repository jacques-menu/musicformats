#!//Users/jacquesmenu/musicformats-git-dev/build/bin/mfsl

###
  This MFSL script contains:
    - a tool specification, telling which MusicFormats tool
      to use, here xml2ly;

    - an input specification, the input source to apply the latter to,
      here file name "Hymn121.xml";

    - a number of MusicFormats options;

    - a SCORE choice specification containing 5 label,
      each telling which options to use
      to get a score generated with this script and xml2ly/LilyPond;

    - a 'select' specification to choose one such SCORE label;
###


# ----------------------------------------------------------
# the MusicFormats tool to be used
# ----------------------------------------------------------

tool : xml2ly


# ----------------------------------------------------------
# the input file to be handled by the tool
# ----------------------------------------------------------

input : "Hymn121.xml"


# ----------------------------------------------------------
# some commmon options
# ----------------------------------------------------------

# header
  -title "Hymn 121 - Angels From The Realms Of Glory"

# display
#   -display-options-values

# LilyPond
  -lilypond-generation-infos

# dynamics
  -all-wedges-below


# ----------------------------------------------------------
# the labels for the SCORE choice
# ----------------------------------------------------------

# choose names that match the intent,
# uppercase to emphasize a choice name is a matter of taste

choice SCORE :
  tutti | soprano | alto | tenor | bass,

  default: tutti
;


# ----------------------------------------------------------
# tell which options are to be used depending on SCORE labels
# ----------------------------------------------------------

case SCORE :
	tutti:
	  -output-file-name "Hymn121_SATB_tutti.ly"

    -global-staff-size 17.675
    -subtitle "Tutti"
  ;

	soprano:
	  -output-file-name "Hymn121_SATB_soprano.ly"

    -subtitle '\markup { "B" \hspace #-0.375 \raise #1.5 {\flat} " instruments" }'
    -keep-musicxml-part-id P1
    -msr-rename-part "P1:soprano"

#     -display-lpsr-short
  ;

	alto:
	  -output-file-name "Hymn121_SATB_alto.ly"

    -subtitle "Alto"
    -keep-musicxml-part-id P2
  ;

	tenor:
	  -output-file-name "Hymn121_SATB_tenor.ly"

    -subtitle "Tenor"
    -keep-musicxml-part-id P3

    -global-staff-size 23 # an easier-to-read score is needed
  ;

	bass:
	  -output-file-name "Hymn121_SATB_bass.ly"

    -subtitle "Bass"
    -keep-musicxml-part-id P4

    -ignore-musicxml-lyrics # there's a glitch at the last measure
  ;
;


# ----------------------------------------------------------
# choosing which score(s) to generate
# ----------------------------------------------------------

###
  Comment/uncomment the statements below to choose
  either a particular SCORE or every one of them to be generated.
  Only one of 'select' and 'every' can be used.

  The 'select' statement chooses one particular score, overriding
  the default "tutti" label setting in the 'choice' statement.

  This can also be done with an option to the MFSL interpreter such as:
    -select SCORE:tenor

  The 'every' statement causes all possible scores for the given choice
  to be generated.

  This can also be done with this option to the MFSL interpreter:
    -every SCORE
###

# select SCORE : tutti ; # superflous actually
# select SCORE : soprano ;
# select SCORE : alto ;
# select SCORE : tenor ;
# select SCORE : bass ;

# every SCORE ;

