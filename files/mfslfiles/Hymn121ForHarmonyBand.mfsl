#!//Users/jacquesmenu/musicformats-git-dev/build/bin/mfsl

###
  This MFSL script contain s:
    - a 'tool' specification, telling which MusicFormats tool
      to use, here xml2ly;

    - an 'input' specification, the input source to apply
      the latter to, here file name "Hymn121.xml";

    - any number of MusicFormats options;

    - any number of SCORE 'choice' specifications,
      telling the possible scores
      that can be generated with this script;

    - a 'select' specification to choose one such SCORE;

    - a 'case' specification stating the options blocks to be used
      when running xml2ly to obtain the chosen SCORE,
      in complement to the ones appearing before the case specfication.
###


# ----------------------------------------------------------
# the MusicFormats tool to be used
# ----------------------------------------------------------

tool : xml2ly


# ----------------------------------------------------------
# the input file to be handled by the tool
# ----------------------------------------------------------

input : "Hymn121.xml"


# ----------------------------------------------------------
# the commmon options
# ----------------------------------------------------------

# header
  -title "Hymn 121 - Angels From The Realms Of Glory"

# display
  -display-options-values

# LilyPond
#   -lilypond-generation-infos

# dynamics
  -all-wedges-below


# ----------------------------------------------------------
# the SCORE choice
# ----------------------------------------------------------

# the labels for the SCORE choice

# choose names that matches the intent,
# uppercase to emphasize the choice name is a matter of taste

choice SCORE :
  director |

  part1_C_instruments |

  part2_Bb_instruments |

  part3_Bb_instruments |

  part4_Bb_instruments |
  part4_BassGuitar,

  default: director
;


# ----------------------------------------------------------
# SCORE specific options
# ----------------------------------------------------------

# basic stuff

case SCORE :
	director:
    -global-staff-size 17.675
    -ragged-bottom off
  ;

  part1_C_instruments,
  part2_Bb_instruments,
  part3_Bb_instruments,
  part4_Bb_instruments,
  part4_BassGuitar:
    -ambitus
    -ragged-bottom off
  ;
;


# scores appearance

case SCORE :
	director:
    -global-staff-size 17.675
    -ragged-bottom off
  ;

  part1_C_instruments:
  ;

  part2_Bb_instruments:
  ;

  part3_Bb_instruments:
  ;

  part4_Bb_instruments:
  ;

  part4_BassGuitar:
    -ambitus
    -ragged-bottom off
  ;
;


# scores identification

case SCORE :
	director:
	  -output-file-name
	    "Hymn121_band_director.ly"
    -subtitle
      "Director - in C"
  ;

  part1_C_instruments:
	  -output-file-name
	    "Hymn121_band_part1_C_instruments.ly"
    -subtitle
      "Part 1 - C instruments"
  ;

  part2_Bb_instruments:
	  -output-file-name
	    "Hymn121_band_part2_Bb_instruments.ly"
    -subtitle
      '\markup {"Part 2 - B" \hspace #-0.375 \raise #1.5 {\flat} " instruments" }'
  ;

  part3_Bb_instruments:
	  -output-file-name
	    "Hymn121_band_part3_Bb_instruments.ly"

    -subtitle
      '\markup {"Part 3 - B" \hspace #-0.375 \raise #1.5 {\flat} " instruments" }'
    -global-staff-size 23
      # a more readable score is needed
  ;

  part4_Bb_instruments:
	  -output-file-name
	    "Hymn121_band_part4_Bb_instruments.ly"
    -subtitle
      '\markup {"Part 4 - B" \hspace #-0.375 \raise #1.5 {\flat} " instruments" }'
  ;

  part4_BassGuitar:
	  -output-file-name
	    "Hymn121_band_part4_BassGuitar.ly"

    -subtitle
      "Part 4 - Bass guitar"
  ;
;


# scores contents

case SCORE :
	director:
    -ignore-musicxml-lyrics
      # there's a glitch in the last measure of the bass...
  ;

	part1_C_instruments:
    -keep-musicxml-part-id P1
    -msr-rename-part "P1:partOne_C_instruments"

    -lpsr-staff-instrument-name "Voice 1:C instruments"
    -indent '3.5\cm'
  ;

	part2_Bb_instruments:
    -keep-musicxml-part-id P2
    -msr-rename-part "P2:partTwo_Bb_instruments"

    -indent '2.5\cm'
    -lpsr-staff-instrument-name
      # let's use LilyPond's '\markup' possibilities
      # successive MFSL character strings are concatenated into a single one
      "Voice 2"
      ":"
      '\markup {'
        '\center-column {'
          '\line { "B" \hspace #-0.375 \raise #1.5 {\flat} }'
          '\line { "instruments" }'
        '}'
      '}'

    -lilypond-transpose-part-id P2=bes
  ;

	part3_Bb_instruments:
    -keep-musicxml-part-id P3
    -msr-rename-part "P3:partThree_Bb_instruments"

    -indent '3\cm'
    -lpsr-staff-instrument-name
      # let's use LilyPond's '\markup' possibilities
      # successive MFSL character strings are concatenated into a single one
      "Voice 3"
      ":"
      '\markup {'
        '\center-column {'
          '\line { "B" \hspace #-0.375 \raise #1.5 {\flat} }'
          '\line { "instruments" }'
        '}'
      '}'

    -lilypond-transpose-part-id 'P3=bes'
  ;

	part4_Bb_instruments:
    -keep-musicxml-part-id P4
    -msr-rename-part "P4:partFour_Bb_instruments"

    -indent '2.5\cm'
    -lpsr-staff-instrument-name
      # let's use LilyPond's '\markup' possibilities
      # successive MFSL character strings are concatenated into a single one
      "Voice 4"
      ":"
      '\markup {'
        '\center-column {'
          '\line { "B" \hspace #-0.375 \raise #1.5 {\flat} }'
          '\line { "instruments" }'
        '}'
      '}'

    -ignore-musicxml-lyrics
      # there's a glitch in the last measure of the bass...

    -lilypond-transpose-part-id 'P4=bes'
  ;

	part4_BassGuitar:
    -keep-musicxml-part-id P4
    -msr-rename-part "P4:partFour_BassGuitar"

    -indent '2.5\cm'
    -lpsr-staff-instrument-name "Voice 4:Bass Guitar"

    -ignore-musicxml-lyrics
      # there's a glitch in the last measure of the bass...
  ;
;


# ----------------------------------------------------------
# choosing which score(s) to generate
# ----------------------------------------------------------

# run the tool with a particular SCORE label

#   select SCORE : director ;

#   select SCORE : part1_C_instruments ;

#   select SCORE : part2_Bb_instruments ;

#   select SCORE : part3_Bb_instruments ;

#   select SCORE : part4_Bb_instruments ;
#   select SCORE : part4_BassGuitar ;


# run the tool with all the SCORE labels, each one in turn

  every SCORE ;
