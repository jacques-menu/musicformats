#!//Users/jacquesmenu/musicformats-git-dev/build/bin/mfsl

###
  This MFSL script contain s:
    - a tool specification, telling which MusicFormats tool
      to use, here xml2ly;

    - an input specification, the input source to apply
      the latter to, here file name "Hymn121.xml";

    - any number of MusicFormats options;

    - a SCORE choice specifications telling the possible scores
      that can be generated with this script;

    - a 'set' specification to choose one such SCORE among the possible ones;

    - a 'case' specification stating the options to be used
      when running xml2ly to obtain the chosen SCORE,
      in complement to the ones appearing before the case specfication.
###


# ----------------------------------------------------------
# the MusicFormats tool to be used
# ----------------------------------------------------------

tool : xml2ly


# ----------------------------------------------------------
# the input file to be handled by the tool
# ----------------------------------------------------------

input : "Hymn121.xml"


# ----------------------------------------------------------
# the commmon options
# ----------------------------------------------------------

# header
  -title "Hymn 121 - Angels From The Realms Of Glory - Vocal quartet"

# display
  -display-options-values

# LilyPond
  -lilypond-generation-infos

# dynamics
  -all-wedges-below


# ----------------------------------------------------------
# the SCORE Choice
# ----------------------------------------------------------

# the labels for the SCORE choice

choice SCORE : tutti | soprano | alto | tenor | bass ;
  # choose names that matches the intent,
  # uppercase to emphasize is a matter of taste


# ----------------------------------------------------------
# SCORE specific options
# ----------------------------------------------------------

case SCORE :
	tutti:
	  -output-file-name "Hymn121_tutti.ly"

    -global-staff-size 17.675
    -subtitle "Tutti"
  ;

	soprano:
	  -output-file-name "Hymn121_soprano.ly"

    -subtitle "Soprano"
    -keep-musicxml-part-id P1
    -msr-rename-part "P1:soprano"

#     -instrument "Soprano"
    -display-lpsr-short
  ;

	alto:
	  -output-file-name "Hymn121_alto.ly"

    -subtitle "Alto"
    -keep-musicxml-part-id P2
  ;

	tenor:
	  -output-file-name "Hymn121_tenor.ly"

    -subtitle "Tenor"
    -keep-musicxml-part-id P3

    -global-staff-size 23 # a more readable score is needed
  ;

	bass:
	  -output-file-name "Hymn121_bass.ly"

    -subtitle "Bass"
    -keep-musicxml-part-id P4

    -ignore-musicxml-lyrics # there's a glitch at the last measure
  ;
;


# ----------------------------------------------------------
# create only one SCORE or all of them
# ----------------------------------------------------------

###
  The 'only' and 'all' statements below provide default settings.
  They can be overriddenv by options to the MFSL interpreter, such as:
    -only SCORE:tenor
  or:
    -all SCORE

  Comment/uncomment them to select either one SCORE only or all of them
###


# generate a particular SCORE
only SCORE : tenor ;


# generate all the SCORE possibilities in a single step
# all SCORE ;
